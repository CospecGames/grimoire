func sleep(int duration) {
    let startTime = clock();
    loop {
        if(clock() - startTime > duration)
            return;
        yield;
    }
}

func boring(string msg) chan(string) {
    let c = chan(string);
    task(chan(string) c, string msg) {
        int i = 0;
        loop {
            c <- (msg ~ " " ~ i);
            i ++;
            sleep(random(1000));
        }
    }(c, msg);
    return c;
}

func fanIn(chan(string) in1, chan(string) in2) chan(string) {
    let out = chan(string);
    task(chan(string) out, chan(string) in1, chan(string) in2) {
        loop {
            let msg;
            select
            case(msg = <-in1) { out <- msg; }
            case(msg = <-in2) { out <- msg; }
        }
    }(out, in1, in2);
    return out;
}

main {
    let c = fanIn(boring("Joe"), boring("Ann"));
    loop(10) {
        print(<- c);
    }
    print("You're boring; I'm leaving.");
} 